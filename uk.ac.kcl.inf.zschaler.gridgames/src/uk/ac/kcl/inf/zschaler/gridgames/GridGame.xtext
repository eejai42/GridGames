grammar uk.ac.kcl.inf.zschaler.gridgames.GridGame with org.eclipse.xtext.common.Terminals

generate gridGame "http://www.inf.kcl.ac.uk/zschaler/gridgames/GridGame"

/* 
 * This language is meant to serve as a high-level modelling language for arcade-style games with a grid playing field
 * 
 * Just a bit of fun, really :-)
 * 
 * Initially, the main goal is to use this as a source for code generation, generating a complete implementation from a model-level description
 * 
 * TODO Enable keyboard triggers
 * TODO Enable cell movement as an alternative behaviour (to support games like Tetris and Breakout), possibly of cell groups rather than just individual cells
 */

GridGame : 
	"game" name=ID "{"
		(states += GlobalCellStateSpec |
		cells += CellSpecification |
		globalActions += GlobalAction |
		inits += GlobalFieldInitialisation |
		fields += FieldSpecification |
		options += OptionSpecification)+
	"}"
;
              
CellSpecification :
	"cell" name=ID "{"
		members += CellMember*
	"}";
	
CellMember :
	CellDisplaySpec |
	CellVarSpec |
	CellStateSpec
;

CellDisplaySpec :
	"display" "{"
		"as" display_type=("label" | "button")
		(("text" text=STRING) | 
		 ("var" var=[VarSpec]) |
		 ("color" color=("black" | "white" | "yellow" | "red" | "green")))
	"}"
;

VarSpec : 
	CellVarSpec |
	ParamSpec
;

CellVarSpec :
	"var" type=("int"|"String") name=ID	
;

GlobalCellStateSpec :
	"states" name=ID ("(" params += ParamSpec ("," params += ParamSpec)* ")")? "{"
		states+=CellState+
		
		"start" "=" start=[CellState]
	"}"
;

ParamSpec :
	type=("int"|"String"|"Behaviour") name=ID
;

CellStateSpec :
	LocalCellStateSpec |
	CellStateSpecReference
;

LocalCellStateSpec :
	"states" "{"
		states+=CellState+
		
		"start" "=" start=[CellState]
	"}"
;

CellStateSpecReference :
	"states" stateSpec=[GlobalCellStateSpec] ("(" params += Value ("," params += Value)* ")")?
;

Value :
	StringValue |
	IntValue |
	DirectBehaviour |
	VarRefValue
;

StringValue :
	value = STRING 
;

IntValue : 
	value = INT 
;

VarRefValue :
	ref   = [CellVarSpec]
;

CellState :
	name=ID "{"
		display = CellDisplaySpec
		("transitions" "{"
			transitions += TransitionSpec+
		 "}")?
		("onEnter" "{" onEnter += CellStateBehaviour ("," onEnter += CellStateBehaviour)*"}")?
	"}"
;

TransitionSpec :
	"on" trigger=TransitionTriggerSpec "goto" target=[CellState]
;

// TODO Consider introducing triggers based on state changes in the context
TransitionTriggerSpec :
	MouseTrigger |
	ContextTrigger 
;

MouseTrigger :
	left ?= "mouse-left" | "mouse-right" {MouseTrigger}
;

ContextTrigger :
	// Trigger a transition when a cell in the context changes state
	"context" "(" exp = ContextExpression ")"
;

CellStateBehaviour :
	DirectBehaviour |
	BehaviourReference
;

BehaviourReference :
	ref=[VarSpec]
;

DirectBehaviour :
	EndGameBehaviour |
	NoOpBehaviour {NoOpBehaviour}
;

EndGameBehaviour :
	"end-game" "(" message=STRING ")"
;

NoOpBehaviour :
	"nop"
;

GlobalAction :
	"action" name=ID "{" 
		"when" trigger = ContextExpression
		"do" behaviour = DirectBehaviour 
	"}"
;

FieldSpecification :
	"field" name=ID "{" 
		"width" "=" width=INT  
		"height" "=" height=INT 
		field_initialisation = FieldInitialisations? 
    "}"
;

OptionSpecification :
	StartFieldDeclaration | 
	AllowRestartMenu |
	GenerationalContexts
;

AllowRestartMenu:
	"allow_restart" {AllowRestartMenu}
;

StartFieldDeclaration:
	"start" "=" field = [FieldSpecification]
;

GenerationalContexts :
	"contextsTriggerGenerationally" {GenerationalContexts}
;

GlobalFieldInitialisation :
	"init" name=ID ("(" params += ParamSpec ("," params += ParamSpec)* ")")? "{" (initialisations += FieldInitialisation ";")+ "}"
;

FieldInitialisations :
	LocalFieldInitialisations |
	FieldInitialisationsRef
;

FieldInitialisationsRef :
	"init" ref=[GlobalFieldInitialisation] ("(" params += Value ("," params += Value)* ")")?
;

LocalFieldInitialisations :
	"init" "{" (initialisations += FieldInitialisation ";")+ "}" 
;

FieldInitialisation :
	DefaultInitialisation |
	RandomInitialisation |
	ContextInitialisation
;

DefaultInitialisation :
	"default" ":" cell=[CellSpecification]
;

RandomInitialisation :
	"random" ":" cell=[CellSpecification] "=" (count=INT | var=[VarSpec])
;

// FIXME This should support a number of values, one for each variable defined for the cell type
ContextInitialisation :
	"context" ":" cell=[CellSpecification] "check" check=ContextExpression "value" "=" exp=ContextExpression
;

ContextExpression :
	sub_exp += AtomicExpression ("." sub_exp += AtomicExpression )* 
;

AtomicExpression :
	FilterExpression |
	StateFilterExpression |
	CountExpression |
	NotEmptyExpression | 
	EmptyExpression 
;

FilterExpression :
	"filter" "(" cell_type += [CellSpecification] ("|" cell_type += [CellSpecification])* ")"
;

StateFilterExpression :
	"inState" "(" cell_state += [CellState] ("|" cell_state += [CellState])* ")"
;

CountExpression :
	"count" "(" ")" {CountExpression} (op = ("==" | ">" | "<") cmpVal = INT)? 
;

NotEmptyExpression :
	"notEmpty" "(" ")" {NotEmptyExpression}
;

EmptyExpression :
	"empty" "(" ")" {EmptyExpression}
;