grammar uk.ac.kcl.inf.zschaler.gridgames.GridGame with org.eclipse.xtext.common.Terminals

generate gridGame "http://www.inf.kcl.ac.uk/zschaler/gridgames/GridGame"

/* 
 * This language is meant to serve as a high-level modelling language for arcade-style games with a grid playing field
 * 
 * Just a bit of fun, really :-)
 * 
 * Initially, the main goal is to use this as a source for code generation, generating a complete implementation from a model-level description
*/

GridGame : 
	"game" name=ID "{"
		cells += CellSpecification+
		fields += FieldSpecification+
		options += OptionSpecification*
	"}"
;
              
CellSpecification :
	"cell" name=ID "{"
		members += CellMember*
	"}";
	
CellMember :
	CellDisplaySpec |
	CellVarSpec 
;

CellDisplaySpec :
	"display" "{"
		"as" display_type=("label" | "button")
		(("text" text=STRING) | 
		 ("var" var=ID))
	"}"
;

CellVarSpec :
	"var" type=("int"|"String") name=ID	
;

FieldSpecification :
	"field" name=ID "{" 
		"width" "=" width=INT  
		"height" "=" height=INT 
		field_initialisation = FieldInitialisations? 
    "}"
;

OptionSpecification :
	StartFieldDeclaration | 
	AllowRestartMenu
;

AllowRestartMenu:
	"allow_restart" {AllowRestartMenu}
;

StartFieldDeclaration:
	"start" "=" field_name = ID
;

FieldInitialisations :
	"init" "{" (initialisations += FieldInitialisation ";")+ "}" 
;

FieldInitialisation :
	DefaultInitialisation |
	RandomInitialisation |
	ContextInitialisation
;

DefaultInitialisation :
	"default" ":" cell=ID
;

RandomInitialisation :
	"random" ":" cell=ID "=" count=INT
;

// FIXME This should support a number of values, one for each variable defined for the cell type
ContextInitialisation :
	"context" ":" cell=ID "check" check=ContextExpression "value" "=" exp=ContextExpression
;

ContextExpression :
	sub_exp += AtomicExpression ("." sub_exp += AtomicExpression )* 
;

AtomicExpression :
	FilterExpression |
	CountExpression |
	NotEmptyExpression 
;

FilterExpression :
	"filter" "(" cell_type = ID ")"
;

CountExpression :
	"count" "(" ")" {CountExpression}
;

NotEmptyExpression :
	"notEmpty" "(" ")" {NotEmptyExpression}
;